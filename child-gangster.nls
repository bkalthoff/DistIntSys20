breed [ child-gangsters child-gangster ]



child-gangsters-own [
  homeAdress
  next-task
  schoolAdress
  hideoutAdress
  stash-houseAdress
  status
  recruits
  child-to-recruit
  adult-gangster-drugs
  tickStandStill  
  speed
  leavingOffset
  directionOffset
  randomGreyPatch
  stoppedByPolice
  
  sick?
  followedByPolice?
  skipAttempt?
  carriesDrug?
  slinging?
  already-recruited?
  
  adult-gangster-order
]


to setup-agent-child-gangster
  set-default-shape child-gangsters "person"
  create-ordered-child-gangsters number-of-child-gangsters [ ; number-of-child-gangsters changed to integer
    
    set homeAdress one-of houses
    setxy [ xcor ] of homeAdress [ ycor ] of homeAdress
    set schoolAdress min-one-of schools [ distance myself ]
    ;set birthDay 1 + random 9
    ;set age 5 + random 5
    set color yellow
    set status 10 + random 10
    ;set label age
    set recruits 0
    set skipAttempt? false
    set carriesDrug? false
    set slinging? false
    set followedByPolice? false
    set already-recruited? false 
    set hidden? true
    set leavingOffset random 25
    set speed 0.25
    set next-task [ -> child-gangster-at-home ]
    set sick? false
  ]
end

to child-gangster-walk-around
  right random 45
  left random 45 
  forward 0.25
  
  let temp-child-gangster self
  let temp-child-to-recruit one-of children with [recruitAttempt? = false] in-radius 2
  
  get-sick
  
  ifelse(sick? = true)[
    set next-task [-> child-gangster-go-home]
    
  ][
  ifelse(slinging? = true)[
    set next-task [-> child-gangster-go-to-stash-house]
  ]
  
  [
  
  
  ifelse(temp-child-to-recruit != nobody and ( time > 650 or time < 550 ) and ( time > 250 or time < 150 ))[
    ask temp-child-to-recruit[
      
      set recruiter temp-child-gangster
      set recruitAttempt? true 
      set-next-task[-> child-turn-to-child-gangster]
      
      ask temp-child-gangster [
        set child-to-recruit temp-child-to-recruit
        set next-task [-> child-gangster-recruit-attempt]
      ]
    ]
  ] [
    
    if (time + leavingOffset > 1000 or time + leavingOffset < 200)[      
      set next-task [-> child-gangster-go-home]      
    ]
  ]  

  ]
  ]
end

to set-next-task [next]
  set previous-task next-task
  set next-task next
end


to child-gangster-at-home
  if (time + leavingOffset = 800)[
    set hidden? false
    set next-task[-> child-gangster-walk-around]
  ]
  
  if(time = 0)[ set already-recruited? false ]
  
  if (time + leavingOffset = 200)[
    set hidden? false
    set skipAttempt? false
    set next-task[-> child-gangster-go-to-school]
  ]
  get-sick
end


to child-gangster-go-to-school
  face schoolAdress
  set directionOffset random 100 + random -100
  set heading heading + directionOffset
  forward speed
  
  if (distance schoolAdress < speed)[
    set hidden? true
    set followedByPolice? false
    set next-task[-> child-gangster-in-school]
  ]
  
  let currentPatch patch-here
  
  
  let roll random 10
  ifelse(roll < status and skipAttempt? = false)[
    set next-task[-> child-gangster-walk-around]
  ][
    set skipAttempt? true
  ]
  
  
  
  
  
end

to child-gangster-in-school
  if (time + leavingOffset = 600)[
    set hidden? false
    set next-task[-> child-gangster-go-home]
  ]
end



to child-gangster-recruit-attempt
  face child-to-recruit
  ifelse(distance child-to-recruit > 1)[
    forward 0.1
  ] [
    set tickStandStill tickStandStill + 1
    if (tickStandStill = 25)[
      
      if(status > ([time-in-school] of child-to-recruit / 20) and ticks > 1200 and already-recruited? = false )[
        ;  link-agents self child-to-recruit
        set status status + 10
        set already-recruited? true
        ask child-to-recruit[
          set breed child-gangsters      
          set color yellow  
          set status 10 + random 5    
          set recruits 0  
          set next-task [-> child-gangster-walk-around]
        ]
        
      ]
      
      set tickStandStill 0
      set next-task [-> child-gangster-walk-around]
    ]
  ]
end



to child-gangster-go-home
  face homeAdress
  set directionOffset random 100 + random -100
  set heading heading + directionOffset
  forward speed
  
  if (distance homeAdress < speed)[
    set hidden? true
    set followedByPolice? false
    set next-task[-> child-gangster-at-home]
  ]
end



to child-gangster-go-to-stash-house
  face stash-houseAdress
  forward 0.25
  
  if(distance stash-houseAdress < 1)[
    set carriesDrug? true
    set next-task [-> child-gangster-transport-drugs]
  ]
end

to child-gangster-transport-drugs
  face hideoutAdress
  forward 0.25
  
  if(distance hideoutAdress < 1)[
    ask hideoutAdress [
     set drugs drugs + 1 
    ]
    set status status + 10
    set carriesDrug? false
    set slinging? false
    
    let tempHideOutAdress hideoutAdress
    ask adult-gangster-order [
      set pickup-place tempHideOutAdress
      set adult-gangster-drugs-delivered? true
      
    ]
    
    set next-task [-> child-gangster-walk-around]
  ]
  
end

to recieve-delivery-instructions 
  set stash-houseAdress min-one-of stash-houses [distance myself]
  set next-task [-> child-gangster-go-to-stash-house]
  
end

to get-sick 
  if sick? = true[
    if(random 100 = 1)[
    set sick? false
    ;print (word  "child " who " got healthy.")
    ]
  ]
  if sick? = false[
  ifelse(random 300 = 1)
  [set sick? true
  ;print (word  "child " who " got sick.")
  ]
  [set sick? false]
  ]
end
